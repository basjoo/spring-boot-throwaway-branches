buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven { url "https://repo.spring.io/libs-release" }
        maven { url "https://repo.spring.io/libs-milestone" }
        maven { url "https://repo.spring.io/snapshot" }
        maven { url 'https://repo.spring.io/plugins-release' }
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.3.1.RELEASE")
    }
}

plugins {
    id("org.padler.gradle.minify") version "1.5.1"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

sourceCompatibility = 1.8

version = '1.0'

repositories {
    jcenter()
    maven { url "https://repo.spring.io/libs-release" }
    maven { url "https://repo.spring.io/libs-milestone" }
    maven { url "https://repo.spring.io/snapshot" }
    mavenLocal()
}

dependencies {
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.boot:spring-boot-starter-webflux")

    compileOnly("org.projectlombok:lombok")
    annotationProcessor("org.projectlombok:lombok")

    testImplementation("org.springframework.boot:spring-boot-starter-test")
}

minification {
    jsSrcDir = "$rootDir/src/main/resources/js"
    jsDstDir = "$buildDir/resources/main/public/js"

    cssSrcDir = "$rootDir/src/main/resources/css"
    cssDstDir = "$buildDir/resources/main/public/css"

    createJsSourceMaps = true
    createCssSourceMaps = true
}

tasks.minify {
    doLast {
        fileTree("$buildDir/resources/main/public/js")
                .filter { it.name.endsWith(".min.js") }
                .forEach { it.append("\n//# sourceMappingURL=${it.name}.map") }
        fileTree("$buildDir/resources/main/public/css")
                .filter { it.name.endsWith(".min.css") }
                .forEach { it.append("\n/*# sourceMappingURL=${it.name}.map */") }
    }
}

tasks.processResources {
    dependsOn(tasks.minify)
}

wrapper {
    gradleVersion = '6.5'
}
